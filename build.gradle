plugins {
    alias libs.plugins.dependencycheck
    alias libs.plugins.errorprone
    alias libs.plugins.versions
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyCheck {
    scanConfigurations = ['runtimeClasspath']
}

subprojects {
    apply plugin: "java"
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'net.ltgt.errorprone'

    java {
        targetCompatibility = JavaVersion.VERSION_21
        sourceCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
    }

    // Reject all non stable versions
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }

    test {
        useJUnitPlatform()
    }

    // Standard libraries added to all projects
    dependencies {
        errorprone(libs.errorprone.core)
        compileOnly libs.errorprone.annotations

        implementation libs.slf4j

        testImplementation(platform(libs.junit.bom))
        testImplementation libs.junit.jupiter
        testRuntimeOnly libs.junit.platform.launcher

        testImplementation libs.reload4j
    }

}
